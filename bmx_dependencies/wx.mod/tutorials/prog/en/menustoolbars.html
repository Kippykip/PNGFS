<html><head><title>Menus and Toolbars</title>


<link rel="stylesheet" href="../media/format.css" type="text/css">
<meta http-equiv="content-type" content="text/html; charset=utf-8">
<meta name="keywords" content="wxWidgets, tutorial, widgets, menus, toolbars, C++, programming, GUI, multiplatform">
<meta nam="description" content="Introduction to winapi">
<meta name="language" content="en">
<meta name="author" content="jan bodnar">
<meta name="distribution" content="global"></head><body>


<div class="center">

<div>&nbsp;
<a href="index.html">Contents</a></div>

<h1>Menus and Toolbars</h1>

<h3>Menubar</h3>

<p>
A menubar is one of the most visible parts of the GUI application. It
is a group of commands located in various menus. While in console
applications you had to remember all those arcane commands, here we
have most of the commands grouped into logical parts. There are
accepted standards that further reduce the amount of time spending to
learn a new application. To implement a menubar in wxWidgets we need to
have three things. A wxMenuBar, a wxMenu and a wxMenuItem.
</p>

<br>
<img src="../media/menu.jpg" alt="Menubar">
<div class="figure">Figure: Menubar</div>


<h3>Simple menu example</h3>

<p>
Creating a menubar in wxWidgets is very simple. Just a few lines of code.
</p>

<pre class="code"><div class="codehead"><a href="../examples/menu.bmx">menu.bmx</a> &lt;- Open source</div>
SuperStrict
 
Framework wx.wxApp
Import wx.wxFrame
 
New MyApp.Run()
 
Type MyApp Extends wxApp

	Method OnInit:Int()
	
		Local sim:SimpleMenu = SimpleMenu(New SimpleMenu.Create(Null, ..
			wxID_ANY, "Simple Menu", -1, -1, 280, 180))
		sim.Show(True)
 
		Return True
	End Method

End Type

Type SimpleMenu Extends wxFrame
 
    Field menubar:wxMenuBar
    Field file:wxMenu

	Method OnInit()
		
		menubar = New wxMenuBar.Create()
		file = New wxMenu.Create()
		file.Append(wxID_CLOSE, "&Quit")
		menubar.Append(file, "&File")
		SetMenuBar(menubar)
		
		Connect(wxID_CLOSE, wxEVT_COMMAND_MENU_SELECTED, OnQuit)

		Centre()
 
	End Method
	
	Function OnQuit(event:wxEvent)
		wxWindow(event.parent).Close(True)
	End Function
	
End Type
</pre>


<pre class="explanation"> menubar = New wxMenuBar.Create()
</pre>

<p>
First we create a menubar object.
</p>

<pre class="explanation"> file = New wxMenu.Create()
</pre>

<p>
 Next we create a menu object.
</p>


<pre class="explanation"> file.Append(wxID_CLOSE, "&amp;Quit")
</pre>

<p>We append a menu item into the menu object. The first parameter is
the id of the menu item. The second parameter is the name of the menu
item. Here we did not create a wxMenuItem explicitely. It was created
by the Append() method behind the scenes. Later on, we will create a
wxMenuItem manually.
</p>


<pre class="explanation">  menubar.Append(file, "&amp;File")
  SetMenuBar(menubar)
</pre>

<p>After that, we append a menu into the menubar. The &amp; character
creates an accelerator key. The character that follows the &amp; is
underlined. This way the menu is accessible via the alt + F shortcut.
In the end, we call the SetMenuBar() method. This method belongs to the
wxFrame widget. It sets up the menubar. </p>


<br>
<img src="../media/simplemenu.jpg" alt="Simle menu example">
<div class="figure">Figure: Simple menu example</div>



<h4>Submenus</h4>

<p>
Each menu can also have a submenu. This way we can group similar
commnads into groups. For example we can place commands that hide/show
various toolbars like personal bar, address bar, status bar or
navigation bar into a submenu called toolbars. Within a menu, we can
seperate commands with a separator. It is a simple line. It is common
practice to separate commands like new, open, save from commands like
print, print preview with a single separator. In our example we will
see, how we can create submenus and menu separators. </p>


<pre class="code"><div class="codehead"><a href="../examples/submenu.bmx">submenu.bmx</a></div>
SuperStrict
 
Framework wx.wxApp
Import wx.wxFrame
 
New MyApp.Run()
 
Type MyApp Extends wxApp

	Method OnInit:Int()
	
		Local sub:SubMenu = SubMenu(New SubMenu.Create(Null, ..
			wxID_ANY, "Submenu", -1, -1, 280, 180))
		sub.Show(True)
 
		Return True
	End Method

End Type

Type SubMenu Extends wxFrame
 
	Field menubar:wxMenuBar
	Field file:wxMenu
	Field imp:wxMenu
	Field quit:wxMenuItem
	
	Method OnInit()
		
		menubar = New wxMenuBar.Create()
		file = New wxMenu.Create()
		file.Append(wxID_CLOSE, "&Quit")
		file.Append(wxID_ANY, "&New")
		file.Append(wxID_ANY, "&Open")
		file.Append(wxID_ANY, "&Save")
		file.AppendSeparator();
		
		imp = New wxMenu.Create()
		imp.Append(wxID_ANY, "Import newsfeed list...")
		imp.Append(wxID_ANY, "Import bookmarks...")
		imp.Append(wxID_ANY, "Import mail...")
		
		file.AppendSubMenu(imp, "I&mport")
		
		quit = New wxMenuItem.Create(file, wxID_CLOSE, "&Quit~tCtrl+W")
		file.AppendItem(quit)
		
		menubar.Append(file, "&File")
		SetMenuBar(menubar)
		
		Connect(wxID_CLOSE, wxEVT_COMMAND_MENU_SELECTED, OnQuit)

		Centre()
 
	End Method
	
	Function OnQuit(event:wxEvent)
		wxWindow(event.parent).Close(True)
	End Function
	
End Type
</pre>

<br>

<p>
We created one submenu in a file menu. It is an import submenu, which can be seen in Opera web browser.
</p>

<pre class="explanation"> file.AppendSeparator()
</pre>

<p>
A menu separator line is created calling an <i>AppendSeparator()</i> method.
</p>

<pre class="explanation"> imp = New wxMenu.Create
 imp.Append(wxID_ANY, "Import newsfeed list...")
 imp.Append(wxID_ANY, "Import bookmarks...")
 imp.Append(wxID_ANY, "Import mail...")

 file.AppendSubMenu(imp, "I&amp;mport")
</pre>

<p>
A submenu is created like a normal menu. It is appended with a <i>AppendSubMenu()</i> method.
</p>


<br>
<img src="../media/submenu.jpg" alt="Submenu">
<div class="figure">Figure: Submenu</div>


<h3>Toolbars</h3>

<p>
Menus group all commands that we can use in an application. Toolbars
provide a quick access to the most frequently used commands.
</p>

<pre class="definition"> virtual wxToolBar* CreateToolBar(long style = wxNO_BORDER | wxTB_HORIZONTAL, 
              wxWindowID id = -1, const wxString&amp; name = "toolBar")
</pre>

<p>
To create a toolbar, we call the CreateToolBar() method of the frame widget.
</p>



<h4>A simple toolbar</h4>

<p>
Our first example will create a simple toolbar. 
</p>

<pre class="code"><div class="codehead"><a href="../examples/toolbar.bmx">toolbar.bmx</a></div>
SuperStrict
 
Framework wx.wxApp
Import wx.wxFrame
 
New MyApp.Run()
 
Type MyApp Extends wxApp

	Method OnInit:Int()
	
		Local tb:Toolbar = Toolbar(New Toolbar.Create(Null, wxID_ANY, ..
			"Toolbar", -1, -1, 280, 180))
		tb.Show(True)
 
		Return True
	End Method

End Type

Type Toolbar Extends wxFrame
 
	Method OnInit()
		
		wxImage.AddHandler( New wxPNGHandler )

		Local bmpExit:wxBitmap = New wxBitmap.CreateFromFile(..
			"../media/exit.png", wxBITMAP_TYPE_PNG)

		Local tbar:wxToolBar = CreateToolBar()
		tbar.AddTool(wxID_CLOSE, "Exit application", bmpExit)
		tbar.Realize()
		
		Connect(wxID_CLOSE, wxEVT_COMMAND_TOOL_CLICKED, OnQuit)
  
		Centre()
 
	End Method
	
	Function OnQuit(event:wxEvent)
		wxWindow(event.parent).Close(True)
	End Function
	
End Type
</pre>

<br>
<p>
In our example, we create a toolbar and one tool button. Clicking on the toolbar button will terminate the application.
</p>

<pre class="explanation"> Local toolbar:wxToolBar = CreateToolBar()
</pre>

<p>
We create a toolbar.
</p>

<pre class="explanation"> toolbar.AddTool(wxID_CLOSE, "Exit application", bmpExit)
</pre>

<p>
We add a tool to the toolbar. 
</p>

<pre class="explanation"> toolbar.Realize()
</pre>

<p>
After we have added the tools, we call the <i>Realize()</i> method.
</p>

<br>
<img src="../media/simpletoolbar.jpg" alt="Toolbar">
<div class="figure">Figure: Toolbar</div>


<h4>Toolbars</h4>

<p>
If we want to have more than one toolbar, we must create them in a different way. e.g. other than calling 
the <i>CreateToolbar()</i> method.
</p>

<pre class="code"><div class="codehead"><a href="../examples/toolbars.bmx">toolbars.bmx</a></div>
SuperStrict
 
Framework wx.wxApp
Import wx.wxFrame
 
New MyApp.Run()
 
Type MyApp Extends wxApp

	Method OnInit:Int()
	
		Local tb:Toolbars = Toolbars(New Toolbars.Create(Null, wxID_ANY, ..
			"Toolbars", -1, -1, 280, 180))
		tb.Show(True)
 
		Return True
	End Method

End Type

Type Toolbars Extends wxFrame
 
	Field toolbar1:wxToolBar
	Field toolbar2:wxToolBar

	Method OnInit()
		
		wxImage.AddHandler( New wxPNGHandler )

		Local bmpExit:wxBitmap = New wxBitmap.CreateFromFile(..
			"../media/exit.png", wxBITMAP_TYPE_PNG)
		Local bmpNew:wxBitmap = New wxBitmap.CreateFromFile(..
			"../media/new.png", wxBITMAP_TYPE_PNG)
		Local bmpOpen:wxBitmap = New wxBitmap.CreateFromFile(..
			"../media/open.png", wxBITMAP_TYPE_PNG)
		Local bmpSave:wxBitmap = New wxBitmap.CreateFromFile(..
			"../media/save.png", wxBITMAP_TYPE_PNG)

		Local vbox:wxBoxSizer = New wxBoxSizer.Create(wxVERTICAL)
		
		toolbar1 = New wxToolBar.Create(Self, wxID_ANY)
		toolbar1.AddTool(wxID_ANY, "", bmpNew)
		toolbar1.AddTool(wxID_ANY, "", bmpOpen)
		toolbar1.AddTool(wxID_ANY, "", bmpSave)
		toolbar1.Realize();
		
		toolbar2 = New wxToolBar.Create(Self, wxID_ANY)
		toolbar2.AddTool(wxID_CLOSE, "Exit application", bmpExit)
		toolbar2.Realize()
		
		vbox.Add(toolbar1, 0, wxEXPAND)
		vbox.Add(toolbar2, 0, wxEXPAND)
		
		SetSizer(vbox);

		
		Connect(wxID_CLOSE, wxEVT_COMMAND_TOOL_CLICKED, OnQuit)
  
		Centre()
 
	End Method
	
	Function OnQuit(event:wxEvent)
		wxWindow(event.parent).Close(True)
	End Function
	
End Type
</pre>

<br>

<p>
In our example, we create two horizontal toolbars. We place them in a vertical box sizer.
</p>


<pre class="explanation"> toolbar1 = New wxToolBar.Create(Self, wxID_ANY)
 ...
 toolbar2 = New wxToolBar.Create(Self, wxID_ANY)
</pre>

<p>
Here we create two toolbars.
</p>

<pre class="explanation">  vbox.Add(toolbar1, 0, wxEXPAND)
  vbox.Add(toolbar2, 0, wxEXPAND)
</pre>

<p>
And here we add them to the vertical box sizer.
</p>


<br>
<img src="../media/toolbars.jpg" alt="Toolbars">
<div class="figure">Figure: Toolbars</div>

<br>

<br>


<center>
<div class="botNav"><span class="botNavItem"><a href="index.html">Contents</a></span> ‡ <span class="botNavItem"><a href="#">Top of Page</a></span></div>
</center>

<div class="footer">
<div class="signature"><span class="copyright">© 2007 Jan Bodnar</span><br>
<span class="copyright"> Adapted for BlitzMax by Bruce A Henderson</span></div>
</div>

</div>
</body></html>