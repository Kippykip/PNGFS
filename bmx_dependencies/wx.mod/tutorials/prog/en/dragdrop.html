<html><head><title>Drag &amp; Drop</title>


<link rel="stylesheet" href="../media/format.css" type="text/css">
<meta http-equiv="content-type" content="text/html; charset=utf-8">
<meta name="keywords" content="wxWidgets, tutorial, drag &amp; drop, C++, programming, GUI, multiplatform">
<meta nam="description" content="Introduction to winapi">
<meta name="language" content="en">
<meta name="author" content="jan bodnar">
<meta name="distribution" content="global"></head><body>


<div class="center">

<div>&nbsp;
<a href="index.html">Contents</a></div>

<h1>Drag and Drop</h1>

<p>
Wikipedia: In computer graphical user interfaces, drag-and-drop is the
action of (or support for the action of) clicking on a virtual object
and dragging it to a different location or onto another virtual object.
In general, it can be used to invoke many kinds of actions, or create
various types of associations between two abstract objects. </p>
<p>
Drag and drop functionality is one of the most visible aspects of the graphical user interface. 
Drag and drop operation enables you to do complex things intuitively. 

</p>

<p>
In drag and drop we basically drag some data from a data source to a data target. 
So we must have:

</p><ul>
<li>Data object</li>
<li>Data source</li>
<li>Data target</li>
</ul>

For drag &amp; drop of text, wxWidgets has a predefined <b>wxTextDropTarget</b> class.

<p></p>


<p>
In the following example, we drag and drop file names from the upper list control to the bottom one.
</p>

<pre class="code"><div class="codehead"><a href="../examples/textdrop.bmx">textdrop.bmx</a> &lt;- Open source</div>
SuperStrict
 
Framework wx.wxApp
Import wx.wxFrame
Import wx.wxTextDropTarget
Import wx.wxGenericDirCtrl
Import wx.wxListCtrl
Import wx.wxTreeCtrl
Import wx.wxSplitterWindow
Import wx.wxTextDataObject
Import wx.wxDropSource

New MyApp.Run()
 
Type MyApp Extends wxApp

	Method OnInit:Int()
	
		Local td:TextDrop = TextDrop(New TextDrop.Create(Null, wxID_ANY, ..
			"TextDrop", -1, -1, 300, 200))
		td.Show(True)
 
		Return True
	End Method

End Type

Type TextDrop Extends wxFrame
 
	Field m_gdir:wxGenericDirCtrl
	Field m_lc1:wxListCtrl
	Field m_lc2:wxListCtrl

	Method OnInit()
		
		Local spl1:wxSplitterWindow = New wxSplitterWindow.Create(..
			Self, -1)
		Local spl2:wxSplitterWindow = New wxSplitterWindow.Create(..
			spl1, -1)
		m_gdir = New wxGenericDirCtrl.Create(spl1, -1, "../..", ..
			-1, -1, -1, -1, wxDIRCTRL_DIR_ONLY)

		m_lc1 = New wxListCtrl.Create(spl2, -1, -1, -1, -1, -1, wxLC_LIST)
		m_lc2 = New wxListCtrl.Create(spl2, -1, -1, -1, -1, -1, wxLC_LIST)

		Local mdt:MyTextDropTarget = New MyTextDropTarget.CreateTarget(m_lc2)
		m_lc2.SetDropTarget(mdt)

		Connect(m_lc1.GetId(), wxEVT_COMMAND_LIST_BEGIN_DRAG, OnDragInit)

		Local tree:wxTreeCtrl = m_gdir.GetTreeCtrl()

		spl2.SplitHorizontally(m_lc1, m_lc2)
		spl1.SplitVertically(m_gdir, spl2)

		Connect(tree.GetId(), wxEVT_COMMAND_TREE_SEL_CHANGED, OnSelect)

		Centre()
 
	End Method

	Function OnSelect(event:wxEvent)
		Local drop:TextDrop = TextDrop(event.parent)
	
		Local filename:String
		Local path:String = drop.m_gdir.GetPath()
		Local dir:Int = ReadDir(path)
		
		If Not dir Then
			Return
		End If
		
		filename = NextFile(dir)

		Local i:Int
		
		drop.m_lc1.ClearAll()
		drop.m_lc2.ClearAll()
		
		While filename
			If FileType(path + "/" + filename) = FILETYPE_FILE Then
			    drop.m_lc1.InsertStringItem(i, filename)
			End If
		    filename = NextFile(dir)
		    i:+ 1
		Wend
		
		CloseDir(dir)
	End Function
	
	Function OnDragInit(event:wxEvent)
		Local drop:TextDrop = TextDrop(event.parent)
	
		Local text:String = drop.m_lc1.GetItemText(wxListEvent(event).GetIndex())
		 
		Local tdo:wxTextDataObject = New wxTextDataObject.Create(text)
		Local tds:wxDropSource = New wxDropSource.Create(tdo, drop.m_lc1)
		tds.DoDragDrop(wxDrag_CopyOnly)

	End Function
	
End Type

Type MyTextDropTarget Extends wxTextDropTarget

	Field m_owner:wxListCtrl
	
	Method CreateTarget:MyTextDropTarget(owner:wxListCtrl)
		m_owner = owner
		Super.Create()
		Return Self
	End Method

	Method OnDropText:Int(x:Int, y:Int, data:String)
		m_owner.InsertStringItem(0, data)
		Return True
	End Method

End Type
</pre>

<br>


<p>
 In our example, we have a window separated into three parts. This is done by the <i>wxSplitterWindow</i> widget.
In the left part of the window, we have a generic directory control. We display all directories available under our
filesystem. In the right part there are two windows. The first displays all files under a selected directory.
The second one is used for dragging the files. 
</p>



<pre class="explanation"> Local mdt:MyTextDropTarget = New MyTextDropTarget.CreateTarget(m_lc2)
 m_lc2.SetDropTarget(mdt)</pre>

<p>
Here we define a text drop target. 
</p>

<pre class="explanation"> Local text:String = m_lc1.GetItemText(event.GetIndex())

 Local tdo:wxTextDataObject = New wxTextDataObject.Create(text)
 Local tds:wxDropSource = New wxDropSource.Create(tdo, m_lc1)
 tds.DoDragDrop(wxDrag_CopyOnly)
</pre>

<p>
In the <i>OnDragInit()</i> method, we define a text data object and a drop source object. We call the <i>DoDragDrop()</i> method. The <i>wxDrag_CopyOnly</i> constant allows only copying of data. 
</p>

<pre class="explanation">Method OnDropText:Int(x:Int, y:Int, data:String)
  m_owner.InsertItem(0, data)
  Return True
End Method
</pre>

<p>
 During the dropping operation, we insert the text data into the list control.
</p>

<br>
<img src="../media/textdrop.jpg" alt="Drag &amp; Drop">
<div class="figure">Figure: Drag &amp; Drop</div>

<br>

<br>

<center>
<div class="botNav"><span class="botNavItem"><a href="index.html">Contents</a></span> ‡ <span class="botNavItem"><a href="#">Top of Page</a></span></div>
</center>

<div class="footer">
<div class="signature"><span class="copyright">© 2007 Jan Bodnar</span><br>
<span class="copyright"> Adapted for BlitzMax by Bruce A Henderson</span></div>
</div>

</div>
</body></html>