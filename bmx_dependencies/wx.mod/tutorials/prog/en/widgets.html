<html><head><title>Widgets</title>


<link rel="stylesheet" href="../media/format.css" type="text/css">
<meta http-equiv="content-type" content="text/html; charset=utf-8">
<meta name="keywords" content="wxWidgets, tutorial, widgets, examples, C++, programming, GUI, multiplatform">
<meta nam="description" content="Introduction to winapi">
<meta name="language" content="en">
<meta name="author" content="jan bodnar">
<meta name="distribution" content="global"></head><body>


<div class="center">

<div>&nbsp;
<a href="index.html">Contents</a></div>

<h1>Widgets</h1>

<p>
In this chapter, we will show small examples of several widgets, available in wxWidgets. Widgets are building
blocks of our applications. wxWidgets consists of a large amout of useful widgets. 
<b>Widget</b> is a basic GUI object by definition. A widget gave
wxWidgets toolkit a name. This term is used on Linux systems. On
Windows, a widget is usually called a control. </p>


<h3>wxCheckBox</h3>

<p>
wxCheckBox is a widget that has two states. On and Off. It is a box
with a label. The label can be set to the right or to the left of the
box. If the checkbox is checked, it is represented by a tick in a box.
A checkbox can be used to show/hide splashscreen at startup, toggle
visibility of a toolbar etc.
</p>


<pre class="code"><div class="codehead"><a href="../examples/checkbox.bmx">checkbox.bmx</a> &lt;- Open source</div>
SuperStrict
 
Framework wx.wxApp
Import wx.wxFrame
Import wx.wxCheckBox
Import wx.wxPanel
 
Const ID_CHECKBOX:Int = 100
 
New MyApp.Run()
 
Type MyApp Extends wxApp

	Method OnInit:Int()
	
		Local cb:CheckBox = CheckBox(New CheckBox.Create(Null, wxID_ANY, ..
			"CheckBox", -1, -1, 270, 150))
		cb.Show(True)
 
		Return True
	End Method

End Type

Type CheckBox Extends wxFrame
 
	Field m_cb:wxCheckBox
	
	Method OnInit()
 
		Local panel:wxPanel = New wxPanel.Create(Self, wxID_ANY)
 
		m_cb = New wxCheckBox.Create(panel, ID_CHECKBOX, "Show title", 20, 20)
		m_cb.SetValue(True)
 
		Connect(ID_CHECKBOX, wxEVT_COMMAND_CHECKBOX_CLICKED, OnToggle)
 
		Centre()
 
	End Method
	
	Function OnToggle(event:wxEvent)
		Local frame:CheckBox = CheckBox(event.parent)
	 
		If frame.m_cb.GetValue() Then
			frame.SetTitle("CheckBox")
		Else
			frame.SetTitle("")
		End If
 

	End Function

End Type
</pre>


<p>
In our example, we display one checkbox on the window. We toggle the titlel of the window by clicking on the 
checkbox.
</p>

<pre class="explanation"> m_cb = New wxCheckBox.Create(panel, ID_CHECKBOX, "Show title", 20, 20)
 m_cb.SetValue(True)</pre>

<p>
We create a checkbox. By default, the title is visible. So we check the checkbox by calling the method <i>SetValue()</i>.
</p>

<pre class="explanation"> Connect(ID_CHECKBOX, wxEVT_COMMAND_CHECKBOX_CLICKED, OnToggle)
</pre>

<p>
If we click on the checkbox, a <i>wxEVT_COMMAND_CHECKBOX_CLICKED</i> event is generated. We connect this event to the
user defined <i>OnToggle()</i> function.
</p>


<pre class="explanation"> If frame.m_cb.GetValue() Then
     frame.SetTitle("CheckBox")
 Else
     frame.SetTitle("")
 End If
</pre>

<p>Inside the OnToggle() method, we check the state of the checkbox. If
it is checked, we display "CheckBox" string in the titlebar, otherwise
we show empty string.
</p>

<br>
<img src="../media/checkbox.jpg" alt="wxCheckBox">
<div class="figure">Figure: wxCheckBox</div>


<h3>wxBitmapButton</h3>

<p>A bitmap button is a button, that displays a bitmap. A bitmap button
can have three other states. Selected, focused and displayed. We can
set a specific bitmap for those states.
</p>


<pre class="code"><div class="codehead"><a href="../examples/bitmapbutton.bmx">bitmapbutton.bmx</a></div>
SuperStrict
 
Framework wx.wxApp
Import wx.wxFrame
Import wx.wxSlider
Import wx.wxBitmapButton
Import wx.wxPanel
 
Const ID_SLIDER:Int = 100
 
New MyApp.Run()
 
Type MyApp Extends wxApp

	Method OnInit:Int()
	
		Local bb:BitmapButton = BitmapButton(New BitmapButton.Create(Null, ..
			wxID_ANY, "BitmapButton", -1, -1, 250, 130))
		bb.Show(True)
 
		Return True
	End Method

End Type
 
Type BitmapButton Extends wxFrame
 
	Field slider:wxSlider
	Field button:wxBitmapButton
	Field pos:Int
	 
	Method OnInit()
 
		wxImage.AddHandler( New wxPNGHandler )
		 
		Local panel:wxPanel = New wxPanel.Create(Self, wxID_ANY)
		slider = New wxSlider.Create(panel, ID_SLIDER, 0, 0, 100, 10, 30, 140, -1)
 
		button = New wxBitmapButton.Create(panel, wxID_ANY, ..
			wxBitmap.CreateFromFile("../media/mute.png", ..
			wxBITMAP_TYPE_PNG), 180, 20)
 
		Connect(ID_SLIDER, wxEVT_COMMAND_SLIDER_UPDATED, OnScroll)
 
		Centre()
 
	End Method
	
	Function OnScroll(event:wxEvent)
		Local frame:BitmapButton = BitmapButton(event.parent)
	
		frame.pos = frame.slider.GetValue()
		
		If frame.pos = 0 Then
			frame.button.SetBitmapLabel(wxBitmap.CreateFromFile(..
				"../media/mute.png", wxBITMAP_TYPE_PNG))
		Else If frame.pos > 0 And frame.pos <= 30 Then
			frame.button.SetBitmapLabel(wxBitmap.CreateFromFile(..
				"../media/min.png", wxBITMAP_TYPE_PNG))
		Else If frame.pos > 30 And frame.pos < 80 Then
			frame.button.SetBitmapLabel(wxBitmap.CreateFromFile(..
				"../media/med.png", wxBITMAP_TYPE_PNG))
		Else
			frame.button.SetBitmapLabel(wxBitmap.CreateFromFile(..
				"../media/max.png", wxBITMAP_TYPE_PNG))
		End If
 
	End Function

End Type
</pre>
<p>
In our example, we have a slider and a bitmap button. We simulate a volume control. 
By dragging the handle of a slider, we change a bitmap on the button.
</p>


<pre class="explanation"> wxImage.AddHandler( New wxPNGHandler )</pre>

<p>
We will use PNG images, so we must initialize a PNG image handler. 
</p>

<pre class="explanation"> button = New wxBitmapButton.Create(panel, wxID_ANY, ..
     wxBitmap.CreateFromFile("../media/mute.png", ..
     wxBITMAP_TYPE_PNG), 180, 20))</pre>

<p>
We create a bitmap button. We specify a bitmap type, in our case <i>wxBITMAP_TYPE_PNG</i>
</p>

<pre class="explanation"> pos = slider.GetValue()</pre>

<p>
We get the slider value. Depending on this value, we set a bitmap for our button. We have four volume states. 
Mute, minimum, medium and maximum. To change a bitmap on the button, we call the <i>SetBitmapLabel()</i> method.
</p>

<br>
<img src="../media/bitmapbutton.jpg" alt="wxBitmapButton" width="290" height="189">
<div class="figure">Figure: wxBitmapButton</div>


<h3>wxToggleButton</h3>

<p>wxToggleButton is a button that has two states. Pressed and not
pressed. You toggle between these two states by clicking on it. There
are situations where this functionality fits well.
</p>

<pre class="code"><div class="codehead"><a href="../examples/togglebutton.bmx">togglebutton.bmx</a></div>
SuperStrict
 
Framework wx.wxApp
Import wx.wxFrame
Import wx.wxToggleButton
Import wx.wxPanel
 
Const ID_TGBUTTON1:Int = 101
Const ID_TGBUTTON2:Int = 102
Const ID_TGBUTTON3:Int = 103
 
New MyApp.Run()
 
Type MyApp Extends wxApp

	Method OnInit:Int()
	
		Local toggle:ToggleButton = ToggleButton(New ToggleButton.Create(Null, ..
			wxID_ANY, "ToggleButton", -1, -1, 280, 180))
		toggle.Centre() 
		toggle.Show(True)
		
		Return True
	End Method

End Type

Type ToggleButton Extends wxFrame
 
	Field m_tgbutton1:wxToggleButton
	Field m_tgbutton2:wxToggleButton
	Field m_tgbutton3:wxToggleButton

	Field m_panel:wxPanel
	Field colour:wxColour

	Method OnInit()
		
		Local panel:wxPanel = New wxPanel.Create(Self, wxID_ANY)
		
		colour = New wxColour.Create(0, 0, 0)
		
		m_tgbutton1 = New wxToggleButton.Create(panel, ..
			ID_TGBUTTON1, "Red", 20, 20)
		m_tgbutton2 = New wxToggleButton.Create(panel, ..
			ID_TGBUTTON2, "Green", 20, 70)
		m_tgbutton3 = New wxToggleButton.Create(panel, ..
			ID_TGBUTTON3, "Blue", 20, 120)
		
		Connect(ID_TGBUTTON1, wxEVT_COMMAND_TOGGLEBUTTON_CLICKED, ..
			OnToggleRed)
		Connect(ID_TGBUTTON2, wxEVT_COMMAND_TOGGLEBUTTON_CLICKED, ..
			OnToggleGreen)
		Connect(ID_TGBUTTON3, wxEVT_COMMAND_TOGGLEBUTTON_CLICKED, ..
			OnToggleBlue)
		
		m_panel = New wxPanel.Create(panel, wxID_NEW, 150, 20, ..
			110, 110, wxSUNKEN_BORDER)
		m_panel.SetBackgroundColour(colour)

 
	End Method

	Function OnToggleRed(event:wxEvent)
		Local tog:ToggleButton = ToggleButton(event.parent)
	
		Local green:Int = tog.colour.Green()
		Local blue:Int = tog.colour.Blue()
		
		If tog.colour.Red() Then
			tog.colour.Set(0, green, blue)
		Else
			tog.colour.Set(255, green, blue)
		End If
		
		tog.m_panel.SetBackgroundColour(tog.colour)
	End Function
	
	Function OnToggleGreen(event:wxEvent)
		Local tog:ToggleButton = ToggleButton(event.parent)
	
		Local red:Int = tog.colour.Red()
		Local blue:Int = tog.colour.Blue()
		
		If tog.colour.Green() Then
			tog.colour.Set(red, 0, blue)
		Else
			tog.colour.Set(red, 255, blue)
		End If
		
		tog.m_panel.SetBackgroundColour(tog.colour)
	End Function

	Function OnToggleBlue(event:wxEvent)
		Local tog:ToggleButton = ToggleButton(event.parent)
	
		Local red:Int = tog.colour.Red()
		Local green:Int = tog.colour.Green()
		
		If tog.colour.Blue() Then
			tog.colour.Set(red, green, 0)
		Else
			tog.colour.Set(red, green, 255)
		End If
		
		tog.m_panel.SetBackgroundColour(tog.colour)
	End Function

	
End Type
</pre>

<p>In our example, we show three toggle buttons and a panel. We set the
background color of the panel to black. The togglebuttons will toggle
the red, green and blue parts of the color value. The background color
will depend on which togglebuttons we have pressed.
</p>


<pre class="explanation"> colour = new wxColour.Create(0, 0, 0)
</pre>

<p>
This is the initial color value. No red, green and blue equals to black. Theoretically speaking, black is not a color after all.
</p>

<pre class="explanation"> m_tgbutton1 = new wxToggleButton.Create(panel, ID_TGBUTTON1, ..
                                 "Red", 20, 20)
</pre>

<p>
 Here we create a toggle button.
</p>


<pre class="explanation"> Connect(ID_TGBUTTON1, wxEVT_COMMAND_TOGGLEBUTTON_CLICKED, ..
     OnToggleRed)
</pre>

<p>
 If we click on the toggle button, a <i>wxEVT_COMMAND_TOGGLEBUTTON_CLICKED</i> event is generated. We connect the
event handlers for this event. Notice, that we don't connect events to the button methods, but to the <i>wxFrame</i>.
widget, which is a grand parent of the toggle buttons. It is possible
to do this, because command events are propagated to their parents. In
our case, button -&gt; panel -&gt; frame. If we wanted to connect the
event to the button, we would have to create our derived button classe,
which would mean more work.
</p>

<pre class="explanation"> If colour.Blue() Then
     colour.Set(red, green, 0)

 Else 
     colour.Set(red, green, 255)
 End If
</pre>

<p>
In the event handlers, we set the respective <i>wxColour</i> parameters.
</p>

<pre class="explanation"> m_panel.SetBackgroundColour(colour)
</pre>

<p>
We set the background of the panel. 
</p>

<br>
<img src="../media/togglebutton.jpg" alt="wxToggleButton" width="320" height="239">
<div class="figure">Figure: wxToggleButton</div>


<h3>wxStaticLine</h3>
<p>
This widget displays a simple line on the window. It can be horizontal or vertical.
</p>

<pre class="code"><div class="codehead"><a href="../examples/staticline.bmx">staticline.bmx</a></div>
SuperStrict
 
Framework wx.wxApp
Import wx.wxDialog
Import wx.wxStaticLine
Import wx.wxStaticText
 
New MyApp.Run()
 
Type MyApp Extends wxApp

	Method OnInit:Int()
	
		Local sl:Staticline = Staticline(New Staticline.Create_(Null, wxID_ANY, ..
			"The Central Europe", -1, -1, 360, 350))
		sl.ShowModal()
		sl.Destroy()
 
		Return True
	End Method

End Type

Type Staticline Extends wxDialog
 
	Method OnInit()
		
		Local font:wxFont = New wxFont.CreateWithAttribs(10, ..
			wxDEFAULT, wxNORMAL, wxBOLD)
		Local heading:wxStaticText = New wxStaticText.Create(Self, ..
			wxID_ANY, "The Central Europe", 30, 15)
		heading.SetFont(font)
		
		Local sl1:wxStaticLine = New wxStaticLine.Create(Self, ..
			wxID_ANY, 25, 50, 300,1)
		
		Local st1:wxStaticText = New wxStaticText.Create(Self, ..
			wxID_ANY, "Slovakia", 25, 80)
		Local st2:wxStaticText = New wxStaticText.Create(Self, ..
			wxID_ANY, "Hungary", 25, 100)
		Local st3:wxStaticText = New wxStaticText.Create(Self, ..
			wxID_ANY, "Poland", 25, 120)
		Local st4:wxStaticText = New wxStaticText.Create(Self, ..
			wxID_ANY, "Czech Republic", 25, 140)
		Local st5:wxStaticText = New wxStaticText.Create(Self, ..
			wxID_ANY, "Germany", 25, 160)
		Local st6:wxStaticText = New wxStaticText.Create(Self, ..
			wxID_ANY, "Slovenia", 25, 180)
		Local st7:wxStaticText = New wxStaticText.Create(Self, ..
			wxID_ANY, "Austria", 25, 200)
		Local st8:wxStaticText = New wxStaticText.Create(Self, ..
			wxID_ANY, "Switzerland", 25, 220)
		
		
		Local st9:wxStaticText = New wxStaticText.Create(Self, ..
			wxID_ANY, "5 379 000", 220, 80, 90, -1, wxALIGN_RIGHT)
		Local st10:wxStaticText = New wxStaticText.Create(Self, ..
			wxID_ANY, "10 084 000", 220, 100, 90, -1, wxALIGN_RIGHT)
		Local st11:wxStaticText = New wxStaticText.Create(Self, ..
			wxID_ANY, "38 635 000", 220, 120, 90, -1, wxALIGN_RIGHT)
		Local st12:wxStaticText = New wxStaticText.Create(Self, ..
			wxID_ANY, "10 240 000", 220, 140, 90, -1, wxALIGN_RIGHT)
		Local st13:wxStaticText = New wxStaticText.Create(Self, ..
			wxID_ANY, "82 443 000", 220, 160, 90, -1, wxALIGN_RIGHT)
		Local st14:wxStaticText = New wxStaticText.Create(Self, ..
			wxID_ANY, "2 001 000",  220, 180, 90, -1, wxALIGN_RIGHT)
		Local st15:wxStaticText = New wxStaticText.Create(Self, ..
			wxID_ANY, "8 032 000",  220, 200, 90, -1, wxALIGN_RIGHT)
		Local st16:wxStaticText = New wxStaticText.Create(Self, ..
			wxID_ANY, "7 288 000",  220, 220, 90, -1, wxALIGN_RIGHT)
		
		Local sl2:wxStaticLine = New wxStaticLine.Create(Self, ..
			wxID_ANY, 25, 260, 300, 1)
		
		Local sum:wxStaticText = New wxStaticText.Create(Self, ..
			wxID_ANY, "164 102 000", 220, 280)

		Local sum_font:wxFont = sum.GetFont()
		sum_font.SetWeight(wxBOLD)
		sum.SetFont(sum_font)

		Centre()
 
	End Method
	
End Type
</pre>

<br>

<p>
In the previous example, we show Centreal European countries and their populations. The use of <i>wxStaticLine</i>
makes it more visually attractive.
</p>


<pre class="explanation"> Local sl1:wxStaticLine = New wxStaticLine.Create(Self, wxID_ANY, 25, 50, 300,1)
</pre>

<p>
 Here we create a horizontal static line. It is 300px wide. The height is 1px.
</p>

<br>
<img src="../media/staticline.jpg" alt="wxStaticLine" width="400" height="409">
<div class="figure">Figure: wxStaticLine</div>


<h3>wxStaticText</h3>

<p>
A wxStaticText widget displays one or more lines of read-only text.
</p>

<pre class="code"><div class="codehead"><a href="../examples/statictext.bmx">statictext.bmx</a></div>
SuperStrict
 
Framework wx.wxApp
Import wx.wxFrame
Import wx.wxPanel
Import wx.wxStaticText
 
New MyApp.Run()
 
Type MyApp Extends wxApp

	Method OnInit:Int()
	
		Local st:StaticText = StaticText(New StaticText.Create(Null, wxID_ANY, ..
			"StaticText", -1, -1, 600, 130))
		st.Show(True)
 
		Return True
	End Method

End Type

Type StaticText Extends wxFrame
 
	Method OnInit()
		
		Local panel:wxPanel = New wxPanel.Create(Self, wxID_ANY)
		Local text:String = "'Cause sometimes you feel tired,~n" + ..
			"feel weak, and when you feel weak, " + ..
			"you feel like you wanna just give up.~n" + ..
			"But you gotta search within you, " + ..
			"you gotta find that inner strength~n" + ..
			"and just pull that shit out of you " + ..
			"and get that motivation to not give up~n" + ..
			"and not be a quitter, " + ..
			"no matter how bad you wanna just fall flat on your face and collapse."
		
		Local st:wxStaticText = New wxStaticText.Create(panel, ..
			wxID_ANY, text, 10, 10, -1, -1, wxALIGN_CENTRE)
		
		Centre()
 
	End Method
	
End Type
</pre>

<br>
<p>
In our example, we display a part of the Eminem's Till I Collapse lyrics on the window.
</p>

<pre class="explanation">   Local st:wxStaticText = New wxStaticText.Create(panel, ..
      wxID_ANY, text, 10, 10, -1, -1, wxALIGN_CENTRE)
</pre>

<p>
Here we create the <i>wxStaticText</i> widget. The static text is aligned to the cetre.
</p>


<br>
<img src="../media/statictext.jpg" alt="wxStaticText" width="640" height="169">
<div class="figure">Figure: wxStaticText</div>


<h3>wxSlider</h3>

<p>wxSlider is a widget that has a simple handle. This handle can be
pulled back and forth. This way we are choosing a value for a specific
task. Sometimes using a slider is more natural, than simply providing a
number or using a spin control.
</p>

<pre class="code"><div class="codehead"><a href="../examples/slider.bmx">slider.bmx</a></div>
SuperStrict
 
Framework wx.wxApp
Import wx.wxFrame
Import wx.wxPanel
Import wx.wxSlider
 
Const ID_SLIDER:Int = 100

New MyApp.Run()
 
Type MyApp Extends wxApp

	Method OnInit:Int()
	
		Local sim:Slider = Slider(New Slider.Create(Null, wxID_ANY, ..
			"Slider", -1, -1, 270, 210))
		sim.Show(True)
 
		Return True
	End Method

End Type

Type Slider Extends wxFrame
 
	Field panel:MyPanel
	
	Method OnInit()
		panel = MyPanel(New MyPanel.Create(Self))
		
		Centre()
 
	End Method
	
End Type

Type MyPanel Extends wxPanel

    Field slider:wxSlider
    Field fill:Int

	Method OnInit()
		fill = 0
		slider = New wxSlider.Create(Self, ID_SLIDER, 0, 0, 140, ..
			50, 30, -1, 140, wxSL_VERTICAL)
		
		Connect(ID_SLIDER, wxEVT_COMMAND_SLIDER_UPDATED, OnScroll)  
		ConnectAny(wxEVT_PAINT, OnPaint)
	End Method

	Function OnScroll(event:wxEvent)
		Local panel:MyPanel = MyPanel(event.parent)
		
		panel.fill = panel.slider.GetValue()
		panel.Refresh()
	End Function
	
	Function OnPaint(event:wxEvent)
		Local panel:MyPanel = MyPanel(event.parent)

		Local dc:wxPaintDC = New wxPaintDC.Create(panel)
		
		Local col1:wxColour = New wxColour.Create(212, 212, 212)
		dc.SetPen(New wxPen.CreateFromColour(col1))
		
		dc.DrawRectangle(140, 30, 80, 140)
		
		Local col2:wxColour = New wxColour.Create(197, 108, 0)
		dc.SetBrush(New wxBrush.CreateFromColour(col2))
		
		dc.DrawRectangle(140, 30, 80, panel.fill)
		
		dc.Free()
	End Function

End Type
</pre>

<p>
In our example, we display a slider widget. By pulling the handle of the slider, we control the background
color of the panel. In such an example, using slider is more natural than using e.g. a spin control. 
</p>

<pre class="explanation"> slider = New wxSlider.Create(Self, ID_SLIDER, 0, 0, 140, 50, 30, ..
    -1, 140, wxSL_VERTICAL)</pre>

<p>
We create a vertical slider. The initial value is 0, minimal value is 0 and maximal value is 140.
We display no ticks and no labels. 
</p>

<pre class="explanation"> Connect(ID_SLIDER, wxEVT_COMMAND_SLIDER_UPDATED, OnScroll) 
</pre>

<p>
Here we connect a <i>wxEVT_COMMAND_SLIDER_UPDATED</i> event to the <i>OnScroll()</i> user defined method.
</p>

<pre class="explanation"> Connect(wxEVT_PAINT, OnPaint)
</pre>

<p>
We will also do some drawing, so we connect <i>OnPaint()</i> method to the <i>wxEVT_PAINT</i> event.
</p>


<pre class="explanation"> fill = slider.GetValue()
 Refresh()
</pre>

<p>
In the <i>OnScroll()</i> method, we will get the current slider value. We call the <i>Refresh()</i> method, which will 
generate a <i>wxEVT_PAINT</i> event.
</p>

<pre class="explanation"> dc.DrawRectangle(140, 30, 80, 140)
 ...
 dc.DrawRectangle(140, 30, 80, fill)
</pre>

<p>
Inside the <i>OnPaint()</i> event handler, we draw two rectangles. The first method is draws a white rectangle with 
a gray border.
The second method draws the a rectangle with some brownish color. The height of the rectangle is controled by the <i>fill</i> value, which is set by the slider widget.
</p>


<br>
<img src="../media/slider.jpg" alt="wxSlider" width="310" height="259">
<div class="figure">Figure: wxSlider</div>


<br>

<br>

<center>
<div class="botNav"><span class="botNavItem"><a href="index.html">Contents</a></span> ‡ <span class="botNavItem"><a href="#">Top of Page</a></span></div>
</center>

<div class="footer">
<div class="signature"><span class="copyright">© 2007 Jan Bodnar</span><br>
<span class="copyright"> Adapted for BlitzMax by Bruce A Henderson</span></div>
</div>

</div>
</body></html>