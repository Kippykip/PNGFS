<html><head><title>Events</title>


<link rel="stylesheet" href="../media/format.css" type="text/css">
<meta http-equiv="content-type" content="text/html; charset=utf-8">
<meta name="keywords" content="wxWidgets, tutorial, events, C++, programming, GUI, multiplatform">
<meta nam="description" content="Events">
<meta name="language" content="en">
<meta name="author" content="jan bodnar">
<meta name="distribution" content="global"></head><body>


<div class="center">

<div>&nbsp;
<a href="index.html">Contents</a></div>

<h1>Events</h1>

<p>
Events are integral part of every GUI application. All GUI applications
are event-driven. An application reacts to different event types which
are generated during it's life. Events are generated mainly by the user
of an application. But they can be generated by other means as well.
e.g. internet connection, window manager, timer.
When the application starts a main loop is created. The application
sits in the main loop and waits for the events to be generated. The
main loop quits, when we exit the application.
</p>

<h3>Definitions</h3>

<p>
<b>Event</b> is a piece of application-level information from the underlying framework, typically the GUI toolkit.
<b>Event loop</b> is a programming construct that waits for and
dispatches events or messages in a program. The event loop repeatedly
looks for events to process them. A <b>dispatcher</b> is a process which maps events to <b>event handlers</b>. Event handlers are methods that react to events.

</p>


<p>
<b>Event object</b> is an object associated with the event. It is usually a window. <b>Event type</b> is a unique event, that has been generated.
</p>


<h3>A simple event example</h3>

<p>
We will talk about a move event.
A move event holds information about move change events.
A move event is generated, when we move a window to a new position. The class that represents the move event is <b>wxMoveEvent</b>. The <b>wxEVT_MOVE</b> is an event type.
</p>

<pre class="code"><div class="codehead"><a href="../examples/move.bmx">move.bmx</a> &lt;- Open source</div>
SuperStrict
 
Framework wx.wxApp
Import wx.wxFrame
Import wx.wxPanel
Import wx.wxStaticText
 
New MyApp.Run()
 
Type MyApp Extends wxApp

	Method OnInit:Int()
	
		Local mv:Move = Move(New Move.Create(Null, wxID_ANY, ..
			"Move event", -1, -1, 250, 130))
		mv.Show(True)
 
		Return True
	End Method

End Type

Type Move Extends wxFrame

	Field st1:wxStaticText
	Field st2:wxStaticText

	Method OnInit()

		Local panel:wxPanel = New wxPanel.Create(Self, -1)
		
		st1 = New wxStaticText.Create(panel, -1, "", 10, 10)
		st2 = New wxStaticText.Create(panel, -1, "", 10, 30)
		
		ConnectAny(wxEVT_MOVE, OnMove)
	
		Centre()
 
	End Method

	Function OnMove(event:wxEvent)
		Local m:Move = Move(event.parent)
	
		Local x:Int, y:Int
		wxMoveEvent(event).GetPosition(x, y)
		
		m.st1.SetLabel("x: " + x)
		m.st2.SetLabel("y: " + y)
	End Function
	
End Type
</pre>
<p>
The example displays the current position of the window.
</p>

<pre class="explanation">ConnectAny(wxEVT_MOVE, OnMove)
</pre>

<p>
Here we connect a <i>wxEVT_MOVE</i> event type with the <i>OnMove()</i> function.
</p>

<pre class="explanation">Local width:Int, height:Int
wxMoveEvent(event).GetPosition(width, height)
</pre>

<p>
The event parameter in the <i>OnMove()</i> function is an object specific to a particular event.
In our case it is the instance of a <i>wxMoveEvent</i> type. Due to limitations of BlitzMax, the parameter
must be declared as a wxEvent type, which we can cast to the correct type.
This object holds information about the event.
We can find out the current position by calling the <i>GetPosition()</i> method of the event.
</p>

<br>
<img src="../media/move.jpg" alt="Move event">
<div class="figure">Figure: Move event</div>


<h3>Event propagation</h3>


<p>
There are two types of events. Basic events and command events. They
differ in propagation. Event propagation is travelling of events from
child widgets to parent widgets and grand parent widgets etc. Basic
events do not propagate. Command events do propagate. For example <i>wxCloseEvent</i> is a basic event.
It does not make sense for this event to propagate to parent widgets. 
</p>

<p>
By default, the event that is catched in a event handler stops propagating. To continue propagation, we must call the <i>Skip()</i> method.
</p>

<pre class="code"><div class="codehead"><a href="../examples/propagate.bmx">propagate.bmx</a></div>
SuperStrict
 
Framework wx.wxApp
Import wx.wxFrame
Import wx.wxPanel
Import wx.wxButton
Import BRL.StandardIO
 
Const ID_BUTTON:Int = 1

New MyApp.Run()
 
Type MyApp Extends wxApp

	Method OnInit:Int()
	
		Local prop:Propagate = Propagate(New Propagate.Create(Null, wxID_ANY, ..
			"Propagate", -1, -1, 250, 130))
		prop.Show(True)
 
		Return True
	End Method

End Type

Type Propagate Extends wxFrame
 
	Method OnInit()
		
		Local panel:MyPanel = MyPanel(New MyPanel.Create(Self, -1))
		
		New MyButton.Create(panel, ID_BUTTON, "Ok")
		
		Connect(ID_BUTTON, wxEVT_COMMAND_BUTTON_CLICKED, OnClick)
		
		Centre()
 
	End Method

	Function OnClick(event:wxEvent)
		Print "event reached frame type"
		event.Skip()
	End Function
	
End Type

Type MyButton Extends wxButton

	Method OnInit()
		Connect(ID_BUTTON, wxEVT_COMMAND_BUTTON_CLICKED, OnClick)
	End Method
	
	Function OnClick(event:wxEvent)
		Print "event reached button type"
		event.Skip()
	End Function
	
End Type

Type MyPanel Extends wxPanel

	Method OnInit()
		Connect(ID_BUTTON, wxEVT_COMMAND_BUTTON_CLICKED, OnClick)
	End Method

	Function OnClick(event:wxEvent)
		Print "event reached panel type"
		event.Skip()
	End Function

End Type
</pre>

<br>

<p>
In our example, we have a button on a panel. The panel is placed in a frame widget. We define a handler for all widgets.
</p>

<pre class="code">
 event reached button type
 event reached panel type
 event reached frame type
</pre>


<p>
We get this, when we click on the button. The event travels from the button to panel and to frame.
</p>
<p>
Try to omit some Skip() methods and see, what hapens. 
</p>




<h3>Vetoing events</h3>

<p>
Sometimes we need to stop processing an event. To do this, we call the method Veto(). 
</p>

<pre class="code"><div class="codehead"><a href="../examples/veto.bmx">veto.bmx</a></div>
SuperStrict
 
Framework wx.wxApp
Import wx.wxFrame
Import wx.wxMessageDialog

New MyApp.Run()
 
Type MyApp Extends wxApp

	Method OnInit:Int()
	
		Local v:Veto = Veto(New Veto.Create(Null, wxID_ANY, ..
			"Veto", -1, -1, 250, 130))
		v.Show(True)
 
		Return True
	End Method

End Type

Type Veto Extends wxFrame
 
	Method OnInit()
		
		ConnectAny(wxEVT_CLOSE_WINDOW, OnClose)
		
		Centre()
 
	End Method
	
	Function OnClose(event:wxEvent)
		Local dial:wxMessageDialog = New wxMessageDialog.Create(Null, ..
			"Are you sure to quit?", "Question", ..
			wxYES_NO | wxNO_DEFAULT | wxICON_QUESTION)

		Local ret:Int = dial.ShowModal()
		dial.Free()

		If ret = wxID_YES Then
			wxWindow(event.parent).Destroy()
		Else
			wxCloseEvent(event).Veto()
		End If

	End Function

End Type
</pre>

<br>


<p>
In our example, we process a <i>wxCloseEvent</i>. This event is
called, when we click the X button on the titlebar, press Alt + F4 or
select close from the system menu. In many applications, we want to
prevent from accidentally closing the window, if we made some changes.
To do this, we must connect the wxEVT_CLOSE_WINDOW event type. </p>

<pre class="explanation"> Local dial:wxMessageDialog = New wxMessageDialog.Create(Null, ..
		"Are you sure to quit?", "Question", ..
		wxYES_NO | wxNO_DEFAULT | wxICON_QUESTION)
</pre>

<p>
During the close event, we show a message dialog.
</p>

<pre class="explanation"> If ret = wxID_YES Then
     wxWindow(event.parent).Destroy()
 Else
     wxCloseEvent(event).Veto()
 End If</pre>

<p>
Depending on the return value, we destroy the window, or veto the event. Notice that to close the window, we must call the <i>Destroy()</i> method. By calling the <i>Close()</i> method, we would end up in an endless cycle.
</p>


<h3>Window identifiers</h3>

<p>
Window identifiers are integers that uniquely determine the window identity in the event system. 
There are three ways to create window id's.
</p>

<ul>
<li>let the system automatically create an id</li>
<li>use standard identifiers</li>
<li>create your own id</li>
</ul>

<p>
Each widget has an id parameter. This is a unique number in the event
system. If we work with multiple widgets, we must differantiate among
them.
</p>

<pre class="definition"> wxButton.Create(parent, -1)
 wxButton.Create(parent, wxID_ANY)
</pre>

<p>If we provide -1 or wxID_ANY for the id parameter, we let the
wxWidgets automatically create an id for us. The automatically created
id's are always negative, whereas user specified id's must always be
positive. We usually use this option when we do not need to change the
widget state. For example a static text, that will never be changed
during the life of the application. We can still get the id, if we
want. There is a method <i>GetId()</i>, which will determine the id for us.
</p>


<p>
Standard identifiers should be used whenever possible. The identifiers
can provide some standard graphics or behaviour on some platforms.
</p>

<pre class="code"><div class="codehead"><a href="../examples/ident.bmx">ident.bmx</a></div>
SuperStrict
 
Framework wx.wxApp
Import wx.wxFrame
Import wx.wxPanel
Import wx.wxButton
 
New MyApp.Run()
 
Type MyApp Extends wxApp

	Method OnInit:Int()
	
		Local id:Ident = Ident(New Ident.Create(Null, wxID_ANY, ..
			"Identifiers", -1, -1, 200, 150))
		id.Show(True)
 
		Return True
	End Method

End Type

Type Ident Extends wxFrame
 
	Method OnInit()
		
		Local panel:wxPanel = New wxPanel.Create(Self, -1)
		
		Local grid:wxGridSizer = New wxGridSizer.Create(2, 3)
		
		grid.Add(New wxButton.Create(panel, wxID_CANCEL), 0, wxTOP | wxLEFT, 9)
		grid.Add(New wxButton.Create(panel, wxID_DELETE), 0, wxTOP, 9)
		grid.Add(New wxButton.Create(panel, wxID_SAVE), 0, wxLEFT, 9)
		grid.Add(New wxButton.Create(panel, wxID_EXIT))
		grid.Add(New wxButton.Create(panel, wxID_STOP), 0, wxLEFT, 9)
		grid.Add(New wxButton.Create(panel, wxID_NEW))
		
		panel.SetSizer(grid)
		Centre()
 
	End Method
	
End Type
</pre>

<br>

<p>
In our example we use standard identifiers on buttons. On linux, the buttons have small icons.
</p>

<br>
<img src="../media/identifiers.jpg" alt="Identifiers">
<div class="figure">Figure: Identifiers</div>

<br>

<br>

<center>
<div class="botNav"><span class="botNavItem"><a href="index.html">Contents</a></span> ‡ <span class="botNavItem"><a href="#">Top of Page</a></span></div>
</center>

<div class="footer">
<div class="signature"><span class="copyright">© 2007 Jan Bodnar</span><br>
<span class="copyright"> Adapted for BlitzMax by Bruce A Henderson</span></div>
</div>

</div>
</body></html>